plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.9"
    id "com.timgroup.jarmangit" version "1.1.86"
}

ext {
    buildNumber = System.getenv("ORIGINAL_BUILD_NUMBER") ?: System.getenv("BUILD_NUMBER")
    githubUrl = "https://github.com/tim-group/gradle-productstore-plugin"
}

group = 'com.timgroup'
if (buildNumber) version = '1.0.' + buildNumber
description = "Publish fat jars to ProductStore"

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation "junit:junit:4.12"
    testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude module: 'groovy'
    }
}

test {
    maxParallelForks = 4
}

gradlePlugin {
    plugins {
        webpack {
            id = "com.timgroup.productstore"
            implementationClass = "com.timgroup.gradle.productstore.ProductStorePublishPlugin"
        }
    }
}

pluginBundle {
    website = githubUrl
    vcsUrl = githubUrl
    description = project.description
    plugins {
        webpack {
            id = "com.timgroup.productstore"
            displayName = "Publish fat jars to ProductStore"
            tags = ["publication"]
        }
    }
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                name = "timgroup"
                url = "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
}

model {
    publishing {
        publications {
            pluginMaven {
                artifact sourcesJar
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)
